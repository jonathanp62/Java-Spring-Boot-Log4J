/*
 * (#)build.gradle  0.1.0   02/07/2025
 *
 * @author   Jonathan Parker
 * @version  0.1.0
 * @since    0.1.0
 *
 * MIT License
 *
 * Copyright (c) 2025 Jonathan M. Parker
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

/*
 * The following system property must be set in HOME/.gradle/gradle.properties:
 *   systemProp.macosArchitecture=intel -or-
 *   systemProp.macosArchitecture=silicon
 */

plugins {
	id 'java'
	id 'org.springframework.boot'			version '3.4.2'
	id 'io.spring.dependency-management'	version '1.1.7'
}

group = 'net.jmp.spring.boot.log4j'
version = '0.1.0'
description = 'Spring-Boot-Log4J'

println "The groupId is ${project.group}"
println "The artifactId is ${project.name}" // Set in settings.gradle
println "The version is ${project.version}"
println "The Gradle version is ${gradle.gradleVersion}"

java.sourceCompatibility = JavaVersion.VERSION_23

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	implementation		(libs.net.jmp.util.logging.logging.utilities)
	implementation		('org.springframework.boot:spring-boot-starter')

	testImplementation	('org.springframework.boot:spring-boot-starter-test')

	testRuntimeOnly		('org.junit.platform:junit-platform-launcher')
}

tasks.withType(Javadoc).configureEach {
    CoreJavadocOptions cjo = options as CoreJavadocOptions
    StandardJavadocDocletOptions sjdo = options as StandardJavadocDocletOptions

    cjo.encoding("UTF-8")
    cjo.addStringOption("source", "23")
    cjo.memberLevel = JavadocMemberLevel.PRIVATE

    sjdo.version(true)
}

tasks.named('test') {
	useJUnitPlatform()

	testLogging {
		events "passed", "skipped", "failed"
	}
}

bootRun {
	mainClass = 'net.jmp.spring.boot.log4j.BootstrapSpring'
	jvmArgs = [ '-Xmx1g' ]

	environment("GRADLE_VERSION", "${gradle.gradleVersion}")
}

tasks.register("bootRunDev") {
	group = "application"
	description = "Runs the Spring Boot application with the dev profile"

	doFirst {
		tasks.bootRun.configure {
			systemProperty("spring.profiles.active", "dev")
		}
	}

	finalizedBy("bootRun")
}

tasks.register("bootRunTest") {
	group = "application"
	description = "Runs the Spring Boot application with the test profile"

	// The 'test' profile is reserved for unit testing

	doFirst {
		tasks.bootRun.configure {
			systemProperty("spring.profiles.active", "testing")
		}
	}

	finalizedBy("bootRun")
}

tasks.register("bootRunProd") {
	group = "application"
	description = "Runs the Spring Boot application with the prod profile"

	doFirst {
		tasks.bootRun.configure {
			systemProperty("spring.profiles.active", "prod")
		}
	}

	finalizedBy("bootRun")
}
